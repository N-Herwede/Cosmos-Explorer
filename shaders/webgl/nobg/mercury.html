<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Procedural Mercury - Noah Herwede</title>
    <style>
        body, html { margin: 0; padding: 0; overflow: hidden; background: transparent; }
        canvas { display: block; width: 100vw; height: 100vh; }
    </style>
</head>
<body>
    <canvas id="glcanvas"></canvas>

    <script id="vs" type="x-shader/x-vertex">
        attribute vec2 position;
        void main() {
            gl_Position = vec4(position, 0.0, 1.0);
        }
    </script>

    <script id="fs" type="x-shader/x-fragment">
        precision highp float;

        uniform vec3 iResolution;
        uniform float iTime;

        // =======================================================================================//
        // PROCEDURAL MERCURY (High-Res and White Paint) - Noah Herwede
        // Features: Craters, High Relief, Bright Ejecta Rays
        // =======================================================================================//

        #define PLANET_POSITION vec3(0, 0, 0)
        #define PLANET_RADIUS 2.0
        #define ROTATION_SPEED 0.05

        mat3 getRotation(float t) {
            float angle = t * ROTATION_SPEED;
            float c = cos(angle), s = sin(angle);
            return mat3(vec3(c, 0, s), vec3(0, 1, 0), vec3(-s, 0, c));
        }
        #define PLANET_ROTATION getRotation(iTime)

        #define COLOR_BASE      vec3(0.22, 0.21, 0.20)
        #define COLOR_DARK      vec3(0.12, 0.11, 0.11)
        #define COLOR_RAYS      vec3(0.55, 0.53, 0.50)
        #define COLOR_FRESH     vec3(0.70, 0.70, 0.72)
        #define COLOR_PAINT     vec3(0.95, 0.95, 0.98)

        #define SUN_DIR normalize(vec3(0.7, 0.35, 0.9))
        #define SUN_COLOR vec3(1.0, 0.98, 0.95) * 4.2
        #define AMBIENT_LIGHT vec3(0.008)

        #define INFINITY 1e10
        #define CAMERA_POSITION vec3(0.0, 0.0, 5.5)

        struct Ray { vec3 origin; vec3 direction; };
        struct Material { vec3 color; float diffuse; float specular; };
        struct Hit { float len; vec3 normal; Material material; };
        struct Sphere { vec3 position; float radius; };

        Hit miss = Hit(INFINITY, vec3(0.), Material(vec3(0.), -1., -1.));
        Sphere planet = Sphere(PLANET_POSITION, PLANET_RADIUS);

        float sphIntersect(in Ray ray, in Sphere sphere) {
            vec3 oc = ray.origin - sphere.position;
            float b = dot(oc, ray.direction);
            float c = dot(oc, oc) - sphere.radius * sphere.radius;
            float h = b * b - c;
            if(h < 0.0) return -1.;
            return -b - sqrt(h);
        }

        float hash(vec3 p) {
            p  = fract( p * 0.3183099 + .1 );
            p *= 17.0;
            return fract( p.x * p.y * p.z * (p.x + p.y + p.z) );
        }

        float noise( in vec3 x ) {
            vec3 i = floor(x);
            vec3 f = fract(x);
            f = f * f * (3.0 - 2.0 * f);
            return mix(mix(mix( hash(i + vec3(0,0,0)), hash(i + vec3(1,0,0)),f.x),
                           mix( hash(i + vec3(0,1,0)), hash(i + vec3(1,1,0)),f.x),f.y),
                       mix(mix( hash(i + vec3(0,0,1)), hash(i + vec3(1,0,1)),f.x),
                           mix( hash(i + vec3(0,1,1)), hash(i + vec3(1,1,1)),f.x),f.y),f.z);
        }

        float fbm(vec3 p, int octaves) {
            float amp = 0.5; float freq = 1.0; float t = 0.0;
            for(int i=0; i<8; i++) {
                if (i >= octaves) break;
                t += noise(p * freq) * amp;
                amp *= 0.5; freq *= 2.0;
            }
            return t;
        }

        float craterProfile(float d, float r) {
            float bowl = smoothstep(r * 0.1, r * 1.2, d);
            bowl = pow(bowl, 0.5);
            float rimWidth = 0.2 * r;
            float rim = smoothstep(r - rimWidth, r, d) * smoothstep(r + rimWidth, r, d);
            return -0.6 * (1.0 - bowl) + rim * 0.15;
        }

        float craterLayer(vec3 p, float density, float seedOffset) {
            vec3 i = floor(p);
            vec3 f = fract(p);
            float h = 0.0;

            for(int k=-1; k<=1; k++) {
                for(int j=-1; j<=1; j++) {
                    for(int l=-1; l<=1; l++) {
                        vec3 neighbor = vec3(float(l), float(j), float(k));
                        vec3 seed = i + neighbor + seedOffset;
                        vec3 rnd = vec3(hash(seed), hash(seed + 11.0), hash(seed + 22.0));

                        vec3 center = neighbor + rnd * 0.7;
                        float d = length(center - f);
                        float radius = 0.15 + rnd.x * 0.5;

                        float irregularity = (noise(p * 4.0 + seed) - 0.5) * 0.1;
                        d += irregularity;

                        if (d < radius * 1.3) {
                            float shape = craterProfile(d, radius);
                            h += shape * smoothstep(1.0, 0.4, d);
                        }
                    }
                }
            }
            return h;
        }

        float getPaintSplatter(vec3 p) {
            vec3 q = p;
            vec3 warp = vec3(
                fbm(p * 2.5, 4),
                fbm(p * 2.5 + vec3(5.2, 1.3, 0.8), 4),
                fbm(p * 2.5 + vec3(2.9, 8.1, 3.4), 4)
            ) * 0.6;

            q += warp;
            float blobs = fbm(q * 2.0, 5);
            return smoothstep(0.50, 0.75, blobs);
        }

        float planetMap(vec3 p) {
            float h = 0.0;
            h += craterLayer(p * 2.0, 1.0, 0.0) * 0.08;
            h += craterLayer(p * 4.5 + vec3(10.0), 1.0, 100.0) * 0.04;
            h += craterLayer(p * 10.0 + vec3(50.0), 1.0, 200.0) * 0.02;
            h += (fbm(p * 8.0, 3) - 0.5) * 0.01;
            return h;
        }

        vec3 planetNormal(vec3 p) {
            float eps = 0.0005;
            vec3 center = PLANET_POSITION;

            vec3 p_rot = (p - center) * PLANET_ROTATION + center;
            float h = planetMap(p_rot);

            float hx = planetMap((p - vec3(eps,0,0) - center) * PLANET_ROTATION + center);
            float hy = planetMap((p - vec3(0,eps,0) - center) * PLANET_ROTATION + center);
            float hz = planetMap((p - vec3(0,0,eps) - center) * PLANET_ROTATION + center);

            vec3 grad = vec3(h-hx, h-hy, h-hz) * 12.0;
            return normalize(normalize(p - center) + grad);
        }

        vec3 baseColor(vec3 n) {
            vec3 p = n * PLANET_RADIUS;
            vec3 rotP = (p - PLANET_POSITION) * PLANET_ROTATION + PLANET_POSITION;

            float h = planetMap(rotP);
            float baseNoise = fbm(rotP * 4.0, 5);
            vec3 col = mix(COLOR_DARK, COLOR_BASE, smoothstep(0.3, 0.7, baseNoise));
            col = mix(col, COLOR_FRESH, smoothstep(0.01, 0.08, h) * 0.4);

            float paintMask = getPaintSplatter(rotP);
            float paintCoverage = smoothstep(0.2, 0.8, paintMask);
            col = mix(col, COLOR_PAINT, paintCoverage);

            return col;
        }

        Hit intersectPlanet(Ray ray) {
            float len = sphIntersect(ray, planet);
            if(len < 0.) return miss;

            vec3 p = ray.origin + len * ray.direction;
            vec3 rotP = (p - PLANET_POSITION) * PLANET_ROTATION + PLANET_POSITION;

            float h = planetMap(rotP);
            len -= h * 0.4;
            p = ray.origin + len * ray.direction;
            rotP = (p - PLANET_POSITION) * PLANET_ROTATION + PLANET_POSITION;

            vec3 normal = planetNormal(p);

            float baseNoise = fbm(rotP * 4.0, 5);
            vec3 col = mix(COLOR_DARK, COLOR_BASE, smoothstep(0.3, 0.7, baseNoise));
            col = mix(col, COLOR_FRESH, smoothstep(0.01, 0.08, h) * 0.4);

            float paintMask = getPaintSplatter(rotP);
            float paintCoverage = smoothstep(0.2, 0.8, paintMask);
            col = mix(col, COLOR_PAINT, paintCoverage);

            float specular = mix(0.05, 0.3, paintCoverage);

            return Hit(len, normal, Material(col, 1., specular));
        }

        vec3 atmosphere(vec3 ro, vec3 rd) {
            float distToPlanet = sphIntersect(Ray(ro, rd), planet);
            if (distToPlanet > 0.0) return vec3(0.0);

            vec3 centerToCam = PLANET_POSITION - ro;
            float distToCenter = length(cross(rd, centerToCam));
            float distFromEdge = distToCenter - PLANET_RADIUS;

            float glow = exp(-distFromEdge * 20.0) * 0.5;
            return vec3(0.9, 0.95, 1.0) * glow;
        }

        vec3 radiance(Ray ray, inout float hitMask) {
            Hit hit = intersectPlanet(ray);

            if(hit.len < INFINITY) {
                hitMask = 1.0;
                Hit shadow = intersectPlanet(Ray(ray.origin + hit.len * ray.direction + 0.01 * SUN_DIR, SUN_DIR));
                float light = step(INFINITY, shadow.len);

                float ndotl = max(0.0, dot(hit.normal, SUN_DIR));

                vec3 col = hit.material.color * (AMBIENT_LIGHT + light * ndotl * SUN_COLOR);

                vec3 halfDir = normalize(SUN_DIR - ray.direction);
                float specAngle = max(0.0, dot(hit.normal, halfDir));
                float specular = pow(specAngle, 30.0);
                col += light * specular * hit.material.specular * SUN_COLOR * 0.5;

                return col;
            } else {
                hitMask = 0.0;
                return atmosphere(ray.origin, ray.direction);
            }
        }

        void mainImage(out vec4 fragColor, in vec2 fragCoord) {
            vec2 uv = (fragCoord/iResolution.xy - 0.5) * iResolution.xy/iResolution.y;
            vec3 ro = CAMERA_POSITION;
            vec3 rd = normalize(vec3(uv, -1.0));

            float hitMask = 0.0;
            vec3 col = radiance(Ray(ro, rd), hitMask);

            col = col / (1.0 + col);
            col = pow(col, vec3(1.0/2.2));

            float alpha = hitMask;
            if (hitMask < 0.5) {
                float distToCenter = length(cross(rd, PLANET_POSITION - ro));
                float edgeDist = max(0.0, distToCenter - PLANET_RADIUS);
                float halo = exp(-edgeDist * 10.5);
                alpha = clamp(halo * 0.40, 0.0, 0.40);
            }

            fragColor = vec4(col, alpha);
        }

        void main() {
            mainImage(gl_FragColor, gl_FragCoord.xy);
        }
    </script>

    <script>
        const canvas = document.getElementById('glcanvas');
        const gl = canvas.getContext('webgl', {alpha: true, premultipliedAlpha: false});
        const PREVIEW_SCALE = new URLSearchParams(window.location.search).get('preview') === '1' ? 0.78 : 1.0;

        if (!gl) { alert('WebGL not supported'); }

        function createShader(gl, type, source) {
            const shader = gl.createShader(type);
            gl.shaderSource(shader, source);
            gl.compileShader(shader);
            if (!gl.getShaderParameter(shader, gl.COMPILE_STATUS)) {
                console.error(gl.getShaderInfoLog(shader));
                gl.deleteShader(shader);
                return null;
            }
            return shader;
        }

        const vsSource = document.getElementById('vs').text;
        const fsSource = document.getElementById('fs').text;

        const vertexShader = createShader(gl, gl.VERTEX_SHADER, vsSource);
        const fragmentShader = createShader(gl, gl.FRAGMENT_SHADER, fsSource);

        const program = gl.createProgram();
        gl.attachShader(program, vertexShader);
        gl.attachShader(program, fragmentShader);
        gl.linkProgram(program);

        if (!gl.getProgramParameter(program, gl.LINK_STATUS)) {
            console.error(gl.getProgramInfoLog(program));
        }

        const positionBuffer = gl.createBuffer();
        gl.bindBuffer(gl.ARRAY_BUFFER, positionBuffer);
        gl.bufferData(gl.ARRAY_BUFFER, new Float32Array([
            -1, -1, 1, -1, -1, 1, -1, 1, 1, -1, 1, 1,
        ]), gl.STATIC_DRAW);

        const positionLocation = gl.getAttribLocation(program, "position");
        gl.enableVertexAttribArray(positionLocation);
        gl.vertexAttribPointer(positionLocation, 2, gl.FLOAT, false, 0, 0);

        gl.useProgram(program);

        const timeLocation = gl.getUniformLocation(program, "iTime");
        const resolutionLocation = gl.getUniformLocation(program, "iResolution");

        function resize() {
            canvas.width = Math.max(1, Math.floor(window.innerWidth * PREVIEW_SCALE));
            canvas.height = Math.max(1, Math.floor(window.innerHeight * PREVIEW_SCALE));
            gl.viewport(0, 0, canvas.width, canvas.height);
            gl.uniform3f(resolutionLocation, canvas.width, canvas.height, 1.0);
        }

        window.addEventListener('resize', resize);
        resize();

        function render(time) {
            gl.uniform1f(timeLocation, time * 0.001);
            gl.drawArrays(gl.TRIANGLES, 0, 6);
            requestAnimationFrame(render);
        }

        requestAnimationFrame(render);
    </script>
</body>
</html>
