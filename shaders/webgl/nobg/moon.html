<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Procedural Moon - Noah Herwede</title>
    <style>
        body, html { margin: 0; padding: 0; overflow: hidden; background: transparent; }
        canvas { display: block; width: 100vw; height: 100vh; }
    </style>
</head>
<body>
    <canvas id="glcanvas"></canvas>

    <script id="vs" type="x-shader/x-vertex">
        attribute vec2 position;
        void main() {
            gl_Position = vec4(position, 0.0, 1.0);
        }
    </script>

    <script id="fs" type="x-shader/x-fragment">
        precision highp float;

        uniform vec3 iResolution;
        uniform float iTime;

        // =======================================================================================//
        // PROCEDURAL MOON - Noah Herwede
        // Features: Maria, Highland Terrain, Crater Detail
        // =======================================================================================//

        #define PLANET_POSITION vec3(0, 0, 0)
        #define PLANET_RADIUS 2.0
        #define ROTATION_SPEED -0.05

        mat3 getRotation(float t) {
            float angle = t * ROTATION_SPEED;
            float c = cos(angle), s = sin(angle);
            return mat3(vec3(c, 0, s), vec3(0, 1, 0), vec3(-s, 0, c));
        }

        #define COLOR_MARE      vec3(0.15, 0.15, 0.16)
        #define COLOR_REGOLITH  vec3(0.28, 0.28, 0.28)
        #define COLOR_EJECTA    vec3(0.38, 0.38, 0.38)

        #define SUN_DIR normalize(vec3(0.4, 0.2, 1.0))
        #define SUN_COLOR vec3(2.75, 2.75, 2.625)
        #define AMBIENT_LIGHT vec3(0.02)

        #define INFINITY 1e10
        #define CAMERA_POSITION vec3(0.0, 0.0, 5.5)

        struct Ray { vec3 origin; vec3 direction; };
        struct Material { vec3 color; float diffuse; float specular; };
        struct Hit { float len; vec3 normal; Material material; };
        struct Sphere { vec3 position; float radius; };

        Hit miss = Hit(INFINITY, vec3(0.), Material(vec3(0.), -1., -1.));
        Sphere planet = Sphere(PLANET_POSITION, PLANET_RADIUS);

        float sphIntersect(in Ray ray, in Sphere sphere) {
            vec3 oc = ray.origin - sphere.position;
            float b = dot(oc, ray.direction);
            float c = dot(oc, oc) - sphere.radius * sphere.radius;
            float h = b * b - c;
            if(h < 0.0) return -1.;
            return -b - sqrt(h);
        }

        float hash(vec3 p) {
            p  = fract( p * 0.3183099 + .1 );
            p *= 17.0;
            return fract( p.x * p.y * p.z * (p.x + p.y + p.z) );
        }

        float noise( in vec3 x ) {
            vec3 i = floor(x);
            vec3 f = fract(x);
            f = f * f * (3.0 - 2.0 * f);
            return mix(mix(mix( hash(i + vec3(0,0,0)), hash(i + vec3(1,0,0)),f.x),
                           mix( hash(i + vec3(0,1,0)), hash(i + vec3(1,1,0)),f.x),f.y),
                       mix(mix( hash(i + vec3(0,0,1)), hash(i + vec3(1,0,1)),f.x),
                           mix( hash(i + vec3(0,1,1)), hash(i + vec3(1,1,1)),f.x),f.y),f.z);
        }

        float fbm(vec3 p, int octaves) {
            float amp = 0.5;
            float freq = 1.0;
            float t = 0.0;
            for(int i=0; i<10; i++) {
                if(i >= octaves) break;
                t += noise(p * freq) * amp;
                amp *= 0.5; freq *= 2.0;
            }
            return t;
        }

        float craterProfile(float d, float r) {
            float bowl = smoothstep(r * 0.2, r * 1.5, d);
            float rim = smoothstep(r * 0.6, r * 1.1, d) * smoothstep(r * 1.6, r * 1.1, d);
            return 1.0 * (bowl - 1.0) + rim * 0.3;
        }

        float craterLayer(vec3 p, float density) {
            vec3 i = floor(p);
            vec3 f = fract(p);
            float h = 0.0;

            for(int k=-1; k<=1; k++) {
                for(int j=-1; j<=1; j++) {
                    for(int l=-1; l<=1; l++) {
                        vec3 neighbor = vec3(float(l), float(j), float(k));
                        vec3 seed = i + neighbor;
                        vec3 rnd = vec3(hash(seed), hash(seed + 11.0), hash(seed + 22.0));
                        vec3 center = neighbor + rnd * 0.7;
                        float d = length(center - f);
                        float radius = 0.3 + rnd.x * 0.4;

                        if (d < radius * 1.5) {
                            float shape = craterProfile(d, radius);
                            h += shape * smoothstep(1.0, 0.2, d);
                        }
                    }
                }
            }
            return h;
        }

        float planetMap(vec3 p) {
            float h = 0.0;
            float continent = fbm(p * 0.6, 3);
            h += craterLayer(p * 2.5, 1.0) * 0.12;
            h += craterLayer(p * 5.5 + 5.0, 1.0) * 0.04;
            h += (fbm(p * 15.0, 2) - 0.5) * 0.004;
            return h;
        }

        float getMareMask(vec3 p) {
            float n = fbm(p * 1.2 + vec3(2.0), 4);
            return smoothstep(0.40, 0.70, n);
        }

        vec3 planetNormal(vec3 p) {
            float eps = 0.005;
            vec3 center = PLANET_POSITION;
            mat3 rot = getRotation(iTime);

            vec3 p_rot = (p - center) * rot + center;
            float h = planetMap(p_rot);

            float hx = planetMap((p - vec3(eps,0,0) - center) * rot + center);
            float hy = planetMap((p - vec3(0,eps,0) - center) * rot + center);
            float hz = planetMap((p - vec3(0,0,eps) - center) * rot + center);

            vec3 grad = vec3(h-hx, h-hy, h-hz) * 8.0;
            return normalize(normalize(p - center) + grad);
        }

        vec3 baseColor(vec3 n) {
            vec3 p = n * PLANET_RADIUS;
            mat3 rot = getRotation(iTime);
            vec3 rotP = (p - PLANET_POSITION) * rot + PLANET_POSITION;

            float mare = getMareMask(rotP);
            vec3 col = mix(COLOR_REGOLITH, COLOR_MARE, mare);

            float vari = fbm(rotP * 10.0, 3);
            col *= 0.9 + 0.2 * vari;

            float dust = fbm(rotP * 20.0, 2);
            col = mix(col, COLOR_EJECTA, smoothstep(0.4, 0.8, dust) * 0.3);

            return col;
        }

        Hit intersectPlanet(Ray ray) {
            float len = sphIntersect(ray, planet);
            if(len < 0.) return miss;

            vec3 p = ray.origin + len * ray.direction;
            mat3 rot = getRotation(iTime);
            vec3 rotP = (p - PLANET_POSITION) * rot + PLANET_POSITION;

            float h = planetMap(rotP);
            len -= h;
            p = ray.origin + len * ray.direction;
            rotP = (p - PLANET_POSITION) * rot + PLANET_POSITION;

            vec3 normal = planetNormal(p);

            float mare = getMareMask(rotP);
            vec3 col = mix(COLOR_REGOLITH, COLOR_MARE, mare);

            float vari = fbm(rotP * 10.0, 3);
            col *= 0.9 + 0.2 * vari;

            float dust = fbm(rotP * 20.0, 2);
            col = mix(col, COLOR_EJECTA, smoothstep(0.4, 0.8, dust) * 0.3);

            return Hit(len, normal, Material(col, 1., 0.0));
        }

        vec3 halo(vec3 ro, vec3 rd) {
            float d = sphIntersect(Ray(ro, rd), planet);
            if(d > 0.0) return vec3(0.0);

            vec3 centerToCam = PLANET_POSITION - ro;
            float distToCenter = length(cross(rd, centerToCam));
            float distFromEdge = distToCenter - PLANET_RADIUS;

            float glow = exp(-distFromEdge * 20.0) * 0.6;
            return vec3(0.9) * glow;
        }

        vec3 radiance(Ray ray, inout float hitMask) {
            Hit hit = intersectPlanet(ray);

            if(hit.len < INFINITY) {
                hitMask = 1.0;
                Hit shadow = intersectPlanet(Ray(ray.origin + hit.len * ray.direction + 0.01 * SUN_DIR, SUN_DIR));
                float light = step(INFINITY, shadow.len);

                float ndotl = max(0.0, dot(hit.normal, SUN_DIR));
                float diffuse = ndotl;

                vec3 col = hit.material.color * (AMBIENT_LIGHT + light * diffuse * SUN_COLOR);
                return col;
            } else {
                hitMask = 0.0;
                return halo(ray.origin, ray.direction);
            }
        }

        void mainImage(out vec4 fragColor, in vec2 fragCoord) {
            vec2 uv = (fragCoord/iResolution.xy - 0.5) * iResolution.xy/iResolution.y;
            vec3 ro = CAMERA_POSITION;
            vec3 rd = normalize(vec3(uv, -1.0));

            float hitMask = 0.0;
            vec3 col = radiance(Ray(ro, rd), hitMask);

            float A = 0.15, B = 0.50, C = 0.10, D = 0.20, E = 0.02, F = 0.30;
            col = ((col*(A*col+C*B)+D*E)/(col*(A*col+B)+D*F)) - E/F;

            col = pow(col, vec3(1.0/2.2));

            float alpha = hitMask;
            if (hitMask < 0.5) {
                float distToCenter = length(cross(rd, PLANET_POSITION - ro));
                float edgeDist = max(0.0, distToCenter - PLANET_RADIUS);
                float halo = exp(-edgeDist * 10.5);
                alpha = clamp(halo * 0.40, 0.0, 0.40);
            }

            fragColor = vec4(col, alpha);
        }

        void main() {
            mainImage(gl_FragColor, gl_FragCoord.xy);
        }
    </script>

    <script>
        const canvas = document.getElementById('glcanvas');
        const gl = canvas.getContext('webgl', {alpha: true, premultipliedAlpha: false});
        const PREVIEW_SCALE = new URLSearchParams(window.location.search).get('preview') === '1' ? 0.78 : 1.0;

        if (!gl) { alert('WebGL not supported'); }

        function createShader(gl, type, source) {
            const shader = gl.createShader(type);
            gl.shaderSource(shader, source);
            gl.compileShader(shader);
            if (!gl.getShaderParameter(shader, gl.COMPILE_STATUS)) {
                console.error(gl.getShaderInfoLog(shader));
                gl.deleteShader(shader);
                return null;
            }
            return shader;
        }

        const vsSource = document.getElementById('vs').text;
        const fsSource = document.getElementById('fs').text;

        const vertexShader = createShader(gl, gl.VERTEX_SHADER, vsSource);
        const fragmentShader = createShader(gl, gl.FRAGMENT_SHADER, fsSource);

        const program = gl.createProgram();
        gl.attachShader(program, vertexShader);
        gl.attachShader(program, fragmentShader);
        gl.linkProgram(program);

        if (!gl.getProgramParameter(program, gl.LINK_STATUS)) {
            console.error(gl.getProgramInfoLog(program));
        }

        const positionBuffer = gl.createBuffer();
        gl.bindBuffer(gl.ARRAY_BUFFER, positionBuffer);
        gl.bufferData(gl.ARRAY_BUFFER, new Float32Array([
            -1, -1, 1, -1, -1, 1, -1, 1, 1, -1, 1, 1,
        ]), gl.STATIC_DRAW);

        const positionLocation = gl.getAttribLocation(program, "position");
        gl.enableVertexAttribArray(positionLocation);
        gl.vertexAttribPointer(positionLocation, 2, gl.FLOAT, false, 0, 0);

        gl.useProgram(program);

        const timeLocation = gl.getUniformLocation(program, "iTime");
        const resolutionLocation = gl.getUniformLocation(program, "iResolution");

        function resize() {
            canvas.width = Math.max(1, Math.floor(window.innerWidth * PREVIEW_SCALE));
            canvas.height = Math.max(1, Math.floor(window.innerHeight * PREVIEW_SCALE));
            gl.viewport(0, 0, canvas.width, canvas.height);
            gl.uniform3f(resolutionLocation, canvas.width, canvas.height, 1.0);
        }

        window.addEventListener('resize', resize);
        resize();

        function render(time) {
            gl.uniform1f(timeLocation, time * 0.001);
            gl.drawArrays(gl.TRIANGLES, 0, 6);
            requestAnimationFrame(render);
        }

        requestAnimationFrame(render);
    </script>
</body>
</html>
